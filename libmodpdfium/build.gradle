buildscript {
    repositories {
        mavenLocal()
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

repositories {
    mavenLocal()
    google()
    jcenter()
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.github.dcendents.android-maven' // 'gradle install' task

android {
    compileSdkVersion 25
    buildToolsVersion "28.0.3"

    // http://stackoverflow.com/questions/35312743/workaround-to-link-a-shared-library-in-debug-mode-with-android-studio-v2
    defaultPublishConfig 'release'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 1
        versionName "25bc9a9-1"
    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jni/lib']
        }
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

signing {
    sign configurations.archives
}

group = "com.github.axet"
archivesBaseName = "libmodpdfium"
version = android.defaultConfig.versionName

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: prop('ossrhUsername'), password: prop('ossrhPassword'))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: prop('ossrhUsername'), password: prop('ossrhPassword'))
            }

            pom.project {
                name 'android libmodpdfium'
                packaging 'jar'
                description 'android libmodpdfium.'
                url 'https://gitlab.com/axet/android-pdfium'

                scm {
                    connection 'scm:git:https://gitlab.com/axet/android-pdfium'
                    developerConnection 'scm:git:https://gitlab.com/axet/android-pdfium'
                    url 'https://gitlab.com/axet/android-pdfium'
                }

                licenses {
                    license {
                        name 'Pdfium LICENCE'
                        url 'https://pdfium.googlesource.com/pdfium/+/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'axet'
                        name 'Alexey Kuznetsov'
                        email 'axet@me.com'
                    }
                }
            }
        }
    }
}
