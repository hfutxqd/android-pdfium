buildscript {
    repositories {
        mavenLocal()
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath 'digital.wup:android-maven-publish:3.6.2'
    }
}

repositories {
    mavenLocal()
    google()
    jcenter()
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'digital.wup.android-maven-publish' // 'gradle publishToMavenLocal' and 'publish' tasks

android {
    compileSdkVersion 25
    buildToolsVersion "28.0.3"

    // http://stackoverflow.com/questions/35312743/workaround-to-link-a-shared-library-in-debug-mode-with-android-studio-v2
    defaultPublishConfig 'release'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode  2
        versionName  "2.0.15"
    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

// https://docs.gradle.org/current/userguide/publishing_maven.html

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

def prop(String name) {
    project.hasProperty(name) ? project.getProperty(name) : null
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.github.axet'
            artifactId 'libmodpdfium'
            version android.defaultConfig.versionName

            from components.android
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'android libmodpdfium'
                description = 'android libmodpdfium.'
                url = 'https://gitlab.com/axet/android-pdfium'
                licenses {
                    license {
                        name = 'Pdfium LICENCE'
                        url = 'https://pdfium.googlesource.com/pdfium/+/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'axet'
                        name = 'Alexey Kuznetsov'
                        email = 'axet@me.com'
                    }
                }
                scm {
                    connection = 'scm:git:' + pom.url.get()
                    developerConnection = 'scm:git:' + pom.url.get()
                    url = pom.url
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username prop('ossrhUsername')
                password prop('ossrhPassword')
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
